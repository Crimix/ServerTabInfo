buildscript {
    repositories {
        jcenter()
        maven {
			name = "forge"
			url = "http://files.minecraftforge.net/maven"
		}
		maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
        maven {
      		url "https://plugins.gradle.org/m2/"
    	}
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '3.+', changing: true
        classpath 'gradle.plugin.com.matthewprenger:CurseGradle:1.4.0'
        classpath "com.wynprice.cursemaven:CurseMaven:2.1.5"
        classpath 'com.github.jengelman.gradle.plugins:shadow:4.0.4'
    }
}

apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'com.matthewprenger.cursegradle'
apply plugin: "com.wynprice.cursemaven"
apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'eclipse'
//Only edit below this line, the above code adds and enables the nessasary things for Forge to be setup.

ext.priv = parseConfig(file('private.properties'))

version = version_major + '.' + version_minor + '.' + version_patch
group = "${mod_group}" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = mod_archivesBaseName + "-" + mc_version

sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '1.8' // Need this here so eclipse task generates correctly.

sourceSets {
    main.resources.srcDirs += 'src/main/generated_resources'
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

minecraft {
    mappings channel: 'snapshot', version: "${mcp_mappings}"
    // makeObfSourceJar = false // an Srg named sources jar is made by default. uncomment this to disable.

    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

    // Default run configurations.
    // These can be tweaked, removed, or duplicated as needed.
    runs {
        client = {
            workingDirectory project.file("run/client")
            // recommended logging data for a userdev environment
            properties 'forge.logging.markers': 'SCAN,REGISTRIES,REGISTRYDUMP'
            // recommended logging level for the console
            properties 'forge.logging.console.level': 'debug'
            mods {
                ServerTabInfo {
                    source sourceSets.main
                }
            }
        }
        server = {
            workingDirectory project.file("run/server")
            // recommended logging data for a userdev environment
            properties 'forge.logging.markers': 'SCAN,REGISTRIES,REGISTRYDUMP'
            // recommended logging level for the console
            properties 'forge.logging.console.level': 'debug'
            mods {
                ServerTabInfo {
                    source sourceSets.main
                }
            }
        }
        data {
            workingDirectory project.file("run/data")
            // Recommended logging data for a userdev environment
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            // Recommended logging level for the console
            property 'forge.logging.console.level', 'debug'
            args '--mod', 'servertabinfo', '--all', '--output', file("src/main/generated_resources"), '--existing', file('src/main/resources/')
            mods {
                ServerTabInfo {
                    source sourceSets.main
                }
            }
        }
    }
}

repositories {
  maven {
	name = "forge"
	url = "http://files.minecraftforge.net/maven"
  }
  maven {
    // location of the maven that hosts JEI files
    name = "Progwml6 maven"
    url = "http://dvs1.progwml6.com/files/maven"
  }
  maven {
    // location of a maven mirror for JEI files, as a fallback
    name = "ModMaven"
    url = "modmaven.k-4u.nl"
  }
    maven {
        name = "GitHubPackages"
        url = uri("https://maven.pkg.github.com/crimix/bml")
        credentials {
            username = priv.githubUser
            password = priv.githubToken
        }
    }
    mavenLocal()
}

configurations {
    shadowCompileOnly
    compile.extendsFrom shadowCompileOnly
}

dependencies {
    minecraft "${project.forge_group}:forge:${project.mc_version}-${project.forge_version}"
    compileOnly fg.deobf("curse.maven:hwyla:3017694")
    runtimeOnly fg.deobf("curse.maven:hwyla:3017694")
    shadowCompileOnly fg.deobf("com.black_dog20.bml:BML:${mc_version}-${bml_version}")
    runtimeOnly fg.deobf("com.black_dog20.bml:BML:${mc_version}-${bml_version}")
}

jar {
    manifest {
        attributes(["Specification-Title": "servertabinfo",
                    "Specification-Vendor": "black_dog20",
                    "Specification-Version": "1",  // We are version 1 of ourselves
                    "Implementation-Title": "Server Tab Info",
                    "Implementation-Version": "${version}",
                    "Implementation-Vendor" :"black_dog20",
                    "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")],)
    }
}

shadowJar {
    configurations = [project.configurations.shadowCompileOnly]
    exclude 'com/black_dog20/bml/Bml.class'
    exclude 'com/black_dog20/bml/internal/**'
    exclude 'assets/blackdogsmoddinglibrary/**'
    exclude 'bml-logo.png'
    minimize()
    relocate 'com.black_dog20.bml', 'com.black_dog20.servertabinfo.repack.bml'


    classifier '' // Replace the default jar
}

reobf {
    shadowJar {}
}

curseforge {
  apiKey = priv.apiKey
  project {
    id = '291788'
    changelogType = 'markdown'
    changelog = 'As always find the changelog here [CHANGELOG](https://github.com/Crimix/ServerTabInfo/blob/master/CHANGELOG.md)'
    releaseType = 'release'
    addGameVersion 'Java 8'
    addGameVersion '1.16.1'
  }
}

def parseConfig(File config) {
    config.withReader {
        def prop = new Properties()
        prop.load(it)
        return (new ConfigSlurper().parse(prop))
    }
}
