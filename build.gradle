buildscript {
    repositories {
        maven { url 'https://maven.minecraftforge.net' }
        mavenCentral()
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1.+', changing: true
        classpath 'gradle.plugin.com.matthewprenger:CurseGradle:1.4.0'
        classpath "net.saliman:gradle-properties-plugin:1.5.1" // To have private properties
        classpath "gradle.plugin.com.github.jengelman.gradle.plugins:shadow:7.0.0"
    }
}

apply plugin: 'net.minecraftforge.gradle'
//Only edit below this line, the above code adds and enables the necessary things for Forge to be setup.
apply plugin: 'com.matthewprenger.cursegradle'
apply plugin: "com.github.johnrengelman.shadow"
apply plugin: 'net.saliman.properties'
apply plugin: 'java'

version = version_major + '.' + version_minor + '.' + version_patch
group = "${mod_group}" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = mod_archivesBaseName + "-" + mc_version

java.toolchain.languageVersion = JavaLanguageVersion.of(8) // Mojang ships Java 8 to end users, so your mod should target Java 8.
sourceSets {
    main.resources.srcDirs += 'src/main/generated_resources'
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

println('Java: ' + System.getProperty('java.version') + ' JVM: ' + System.getProperty('java.vm.version') + '(' + System.getProperty('java.vendor') + ') Arch: ' + System.getProperty('os.arch'))
minecraft {
    mappings channel: 'snapshot', version: "${mcp_mappings}"
    // makeObfSourceJar = false // an Srg named sources jar is made by default. uncomment this to disable.

    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

    // Default run configurations.
    // These can be tweaked, removed, or duplicated as needed.
    runs {
        client = {
            workingDirectory project.file("run/client")
            // recommended logging data for a userdev environment
            properties 'forge.logging.markers': 'SCAN,REGISTRIES,REGISTRYDUMP'
            // recommended logging level for the console
            properties 'forge.logging.console.level': 'debug'

            // Disable mixin refmaps
            property 'mixin.env.disableRefMap', 'true'

            mods {
                ServerTabInfo {
                    source sourceSets.main
                }
            }
        }
        server = {
            workingDirectory project.file("run/server")
            // recommended logging data for a userdev environment
            properties 'forge.logging.markers': 'SCAN,REGISTRIES,REGISTRYDUMP'
            // recommended logging level for the console
            properties 'forge.logging.console.level': 'debug'

            // Disable mixin refmaps
            property 'mixin.env.disableRefMap', 'true'

            mods {
                ServerTabInfo {
                    source sourceSets.main
                }
            }
        }
        data {
            workingDirectory project.file("run/data")
            // Recommended logging data for a userdev environment
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            // Recommended logging level for the console
            property 'forge.logging.console.level', 'debug'
            args '--mod', 'servertabinfo', '--all', '--output', file("src/main/generated_resources"), '--existing', file('src/main/resources/')
            mods {
                ServerTabInfo {
                    source sourceSets.main
                }
            }
        }
    }
}

repositories {
  maven {
    // location of the maven that hosts JEI files
    name = "Progwml6 maven"
    url = "https://dvs1.progwml6.com/files/maven"
  }
  maven {
    // location of a maven mirror for JEI files, as a fallback
    name = "ModMaven"
    url = "https://modmaven.k-4u.nl"
  }
    maven {
        name = "GitHubPackages"
        url = uri("https://maven.pkg.github.com/crimix/bml")
        credentials {
            username = github_user
            password = github_token
        }
        content {
            includeGroup "com.black_dog20.bml"
        }
    }
    maven {
        url "https://www.cursemaven.com"
        content {
            includeGroup "curse.maven"
        }
    }
    mavenLocal()
}

configurations {
    shadowCompileOnly
    compileOnly.extendsFrom(shadowCompileOnly)
}

dependencies {
    minecraft "${project.forge_group}:forge:${project.mc_version}-${project.forge_version}"
    compileOnly fg.deobf("curse.maven:hwyla-253449:3033593")
    runtimeOnly fg.deobf("curse.maven:hwyla-253449:3033593")
    shadowCompileOnly fg.deobf("com.black_dog20.bml:bml:${bml_version}")
    runtimeOnly fg.deobf("com.black_dog20.bml:bml:${bml_version}")
}

jar {
    manifest {
        attributes(["Specification-Title": "servertabinfo",
                    "Specification-Vendor": "black_dog20",
                    "Specification-Version": "1",  // We are version 1 of ourselves
                    "Implementation-Title": "Server Tab Info",
                    "Implementation-Version": "${version}",
                    "Implementation-Vendor" :"black_dog20",
                    "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")],)
    }
}

shadowJar {
    configurations = [project.configurations.shadowCompileOnly]
    exclude 'com/black_dog20/bml/Bml.class'
    exclude 'com/black_dog20/bml/internal/**'
    exclude 'assets/blackdogsmoddinglibrary/**'
    exclude 'bml-logo.png'
    exclude 'bml.mixins.json'
    exclude 'bml.refmap.json'
    minimize()
    relocate 'com.black_dog20.bml', 'com.black_dog20.servertabinfo.repack.bml'


    classifier '' // Replace the default jar
}

reobf {
    shadowJar {}
}

// this replaces jar.finalizedBy('reobfJar') in the standard forge mod buildscript
tasks.build.dependsOn reobfShadowJar
jar.finalizedBy('reobfShadowJar')

curseforge {
  apiKey = curse_apiKey
  project {
    id = '291788'
    changelogType = 'markdown'
    changelog = 'As always find the changelog here [CHANGELOG](https://github.com/Crimix/ServerTabInfo/blob/1.16.4/CHANGELOG.md)'
    releaseType = 'release'
    addGameVersion 'Java 8'
    addGameVersion '1.16.4'
    addGameVersion '1.16.5'
      relations {
          optionalDependency  'permission-levels'
      }
  }
}
